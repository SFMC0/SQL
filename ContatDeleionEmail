<!--%%[
SET @totalDeleted = 0

/* Get the total count of deletion from Archvie DE */

SET @ContactsSubmittedForDeletion =  DataExtensionRowCount("ENT.2025_ContactDeletion_Archive")

/* Previous day boundaries */
SET @previousDate = FormatDate(DateAdd(Now(), -1, "D"), "yyyy-MM-dd")
SET @dayStart = Concat(@previousDate, " 00:00:00")
SET @dayEnd = Concat(@previousDate, " 23:59:59")
SET @currentYear = FormatDate(Now(), "yyyy")

SET @rows = LookupOrderedRows("ENT.ContactsDeletion_APIDeleteLog_v01", 0, "SFMC_operationID DESC", "LookupRow", "1")
SET @rowCount = RowCount(@rows)

FOR @i = 1 TO @rowCount DO

  SET @row = Row(@rows, @i)
  SET @startDT = Field(@row, 'ActualDeleteStartDate')
  SET @endDT = Field(@row, 'ActualDeleteCompleteDate')
  SET @count = Field(@row, 'DeletedRecordCount')

  IF NOT EMPTY(@startDT) AND NOT EMPTY(@endDT) AND NOT EMPTY(@count) THEN

    SET @startDateOnly = FormatDate(@startDT, "yyyy-MM-dd")
    SET @endDateOnly = FormatDate(@endDT, "yyyy-MM-dd")
    SET @startYear = FormatDate(@startDT, "yyyy")
    SET @endYear = FormatDate(@endDT, "yyyy")

    /* Fully within previous day */
    IF @startDateOnly == @previousDate AND @endDateOnly == @previousDate THEN
      SET @totalDeleted = Add(@totalDeleted, @count)

    /* Spanning multiple days */
    ELSEIF @startYear == @currentYear AND @endYear == @currentYear AND @startDateOnly != @endDateOnly THEN

      IF @endDT > @startDT THEN

        SET @hoursDiff = DateDiff(@startDT, @endDT, "H")
        IF @hoursDiff < 1 THEN SET @hoursDiff = 1 ENDIF

        SET @deletionPerHour = Divide(@count, @hoursDiff)

        /* Calculate overlap with previous day */
        SET @effectiveStart = IIF(@startDT > @dayStart, @startDT, @dayStart)
        SET @effectiveEnd = IIF(@endDT < @dayEnd, @endDT, @dayEnd)

        IF @effectiveEnd > @effectiveStart THEN
          SET @overlapHours = DateDiff(@effectiveStart, @effectiveEnd, "H")
          IF @overlapHours < 1 THEN SET @overlapHours = 1 ENDIF

          SET @partialDeleted = Multiply(@deletionPerHour, @overlapHours)
          SET @totalDeleted = Add(@totalDeleted, @partialDeleted)
        ENDIF

      ENDIF
    ENDIF
  ENDIF
NEXT @i
]%%-->

<!DOCTYPE html>
<html>
<head>
    <title>Daily Contacts Deletion Summary</title>
    <style>
        body { font-family: Arial, sans-serif; line-height: 1.6; }
        .header { background-color: #f4f4f4; padding: 20px; text-align: center; }
        .content { padding: 20px; }
        .footer { background-color: #f4f4f4; padding: 10px; text-align: center; font-size: 12px; }
        .highlight { font-weight: bold; color: #d9534f; }
    </style>
</head>
<body>
    <div class="header">
        <h1>Daily Contacts Deletion Summary</h1>
        <p>Report Date: %%=FormatDate(DateAdd(Now(), -1, "D"), "MMMM dd, yyyy")=%% </p>
    </div>
    
    <div class="content">
        <h2>Yesterday's Contacts Deletion Summary</h2>
        
          <p><strong>Total Contacts Deleted:</strong> <span style="color: #d9534f;">%%=FormatNumber(@totalDeleted, "N0")=%%</span></p>
      
      <h4>
        Total number of contacts submitted for deletion: : <Strong>%%=FormatNumber(@ContactsSubmittedForDeletion, "N0")=%%</Strong>
      </h4>

        
        <p>This report includes:</p>
        <ul>
            <li>Deletion jobs that completed entirely within yesterday</li>
            <li>Proportional count from deletion jobs that spanned multiple days</li>
            <li>If the deletion is ongoing at the time of this email's generation, that batch will be excluded from the report.</li>
        </ul>
        
        <p>For detailed deletion logs, please check the Deletion Data Extension in Marketing Cloud:  <em>ContactsDeletion_APIDeleteLog_v01</em>.</p>
    </div>
    
    <div class="footer">
        <p>This is an automated report. Please contact CE Marvels team if you have any questions.</p>
    </div>
</body>
    <custom name="opencounter" type="tracking"/>

</html>
